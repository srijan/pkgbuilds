# Maintainer: Srijan Choudhary <srijan4@gmail.com>
# Contributor: Jeffrey Gelens <jgelens@gmail.com>
pkgname=haproxy
pkgver=1.4.23
pkgrel=3
pkgdesc="The Reliable, High Performance TCP/HTTP Load Balancer"
arch=('i686' 'x86_64')
url="http://haproxy.1wt.eu"
license=('GPL')
depends=('pcre')
makedepends=('gcc>=4.2.0' 'linux-headers')
backup=('etc/haproxy/haproxy.cfg')
install="${pkgname}.install"
source=(  "http://haproxy.1wt.eu/download/1.4/src/${pkgname}-${pkgver}.tar.gz"
          "haproxy.init"
          "haproxy.service" )
md5sums=('6535d5e58037ada4b58b439cebe03c79'
         '3da8258f921eb8703dadf95f651bd82f'
         '3cf5584eb54f41f734ee794ef19b0160')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make TARGET=linux2628 USE_PCRE=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX="${pkgdir}/usr" SBINDIR="${pkgdir}/usr/bin" DOCDIR="${pkgdir}/usr/share/doc/${pkgname}" install

  install -D -m755 "${startdir}/haproxy.init" "${pkgdir}/etc/rc.d/haproxy"
  install -D -m644 "examples/haproxy.cfg" "${pkgdir}/etc/haproxy/haproxy.cfg"
  install -D -m644 "${startdir}/haproxy.service" "${pkgdir}/usr/lib/systemd/system/haproxy.service"
}

# vim:ts=2:sw=2:et
